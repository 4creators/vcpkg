include(vcpkg_common_functions)
vcpkg_from_github(
    OUT_SOURCE_PATH SOURCE_PATH
    REPO google/flatbuffers
    REF v1.8.0
    SHA512 8f6c84caa6456418fc751ea9de456dd37378b3239d1a41d2205140e7b19a5b8b2e342a22dc8d7fdd0c36878455e9d7401cc6438d3b771f7875e8fcfe7bbd52f1
    HEAD_REF master
)

file(READ "${SOURCE_PATH}/CMakeLists.txt" _contents)
string(REPLACE "RUNTIME DESTINATION \${CMAKE_INSTALL_LIBDIR}" "RUNTIME DESTINATION \${CMAKE_INSTALL_BINDIR}" _contents "${_contents}")
string(REPLACE "EXPORT FlatcTargets\n      RUNTIME DESTINATION \${CMAKE_INSTALL_BINDIR}" "EXPORT FlatcTargets\n      RUNTIME DESTINATION tools/flatc" _contents "${_contents}")
file(WRITE "${SOURCE_PATH}/CMakeLists.txt" "${_contents}")

string(COMPARE NOTEQUAL "${VCPKG_CMAKE_SYSTEM_NAME}" "WindowsStore" FLATBUFFERS_BUILD_FLATC)

string(COMPARE EQUAL "${VCPKG_LIBRARY_LINKAGE}" "dynamic" FLATBUFFERS_BUILD_SHAREDLIB)
string(COMPARE EQUAL "${VCPKG_LIBRARY_LINKAGE}" "static" FLATBUFFERS_BUILD_FLATLIB)

vcpkg_configure_cmake(
    SOURCE_PATH ${SOURCE_PATH}
    PREFER_NINJA
    OPTIONS
        -DCMAKE_WINDOWS_EXPORT_ALL_SYMBOLS=ON
        -DFLATBUFFERS_BUILD_TESTS=OFF
        -DFLATBUFFERS_BUILD_GRPCTEST=OFF
        -DFLATBUFFERS_BUILD_SHAREDLIB=${FLATBUFFERS_BUILD_SHAREDLIB}
        -DFLATBUFFERS_BUILD_FLATLIB=${FLATBUFFERS_BUILD_FLATLIB}
        -DFLATBUFFERS_BUILD_FLATHASH=OFF
    OPTIONS_RELEASE
        -DFLATBUFFERS_BUILD_FLATC=${FLATBUFFERS_BUILD_FLATC}
    OPTIONS_DEBUG
        -DFLATBUFFERS_BUILD_FLATC=OFF
)

vcpkg_install_cmake()

vcpkg_fixup_cmake_targets(CONFIG_PATH lib/cmake/flatbuffers)
vcpkg_copy_tool_dependencies(tools/flatc)

file(REMOVE_RECURSE ${CURRENT_PACKAGES_DIR}/debug/include)

# Handle copyright
file(COPY ${SOURCE_PATH}/LICENSE.txt DESTINATION ${CURRENT_PACKAGES_DIR}/share/flatbuffers)
file(RENAME ${CURRENT_PACKAGES_DIR}/share/flatbuffers/LICENSE.txt ${CURRENT_PACKAGES_DIR}/share/flatbuffers/copyright)
